// SPDX-License-Identifier: MIT
pragma solidity >=0.6.0 <0.8.0;

/// @title An implementation for liquidity-sensitive LMSR market maker in Solidity
/// @author Marto (based on Abdulla Al-Kamil LsLMSR implementation)
/// @dev Feel free to make any adjustments to the code

import "@openzeppelin/contracts/token/ERC1155/IERC1155Receiver.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/token/ERC20/SafeERC20.sol";
import {ABDKMath} from "./ABDKMath64x64.sol";
import "./ConditionalTokens.sol";

contract PrecogMarketV2 is IERC1155Receiver, Ownable {
    /**
     * @dev: the contract uses the ABDKMath library to allow for math functions such as logarithms and exponents.
     * As such, all the sensitive state variables are stored as int128(signed 64.64 bit fixed point number).
     */
    using SafeERC20 for IERC20;

    struct ShareInfo {
        uint256 buys;
        uint256 outcomeOneBalance;
        uint256 outcomeTwoBalance;
        uint256 deposited;
        uint256 withdrawn;
        uint256 claimed;
    }

    uint256 public startTimestamp;
    uint256 public endTimestamp;
    bytes32 public condition;
    address public token;
    mapping(address => ShareInfo) public accountShares;

    uint public numOutcomes;
    int128[] private q;
    int128 private b;
    int128 private alpha;
    int128 private currentCost;
    int128 private totalShares;
    uint private totalBuys;
    ConditionalTokens private CT;
    bool private initialized;

    modifier onlyAfterInit {
        require(initialized == true);
        _;
    }

    modifier onlyOwnerOrSelf() {
        require(_msgSender() == owner() || _msgSender() == address(this), "Ownable: caller is not the owner or self");
        _;
    }
    /**
     * @notice Constructor function for the market maker
     * @param _ct The address for the deployed conditional tokens contract
     * @param _token Which ERC-20 token will be used to purchase and redeem outcome tokens for this condition
     */
    constructor(address _ct, address _token) {
        CT = ConditionalTokens(_ct);
        token = _token;
    }

    /**
     * @notice Set up some of the variables for the market maker
     * @param _oracle The address for the EOA/contract which will act as the oracle for this condition
     * @param _questionId The question ID (needs to be unique)
     * @param _numOutcomes The number of different outcomes available
     * _subsidyToken Which ERC-20 token will be used to purchase and redeem outcome tokens for this condition
     * @param _subsidy How much initial funding is used to seed the market maker.
     * @param _overround How much 'profit' does the AMM claim? Note that this is
     * represented in bips. Therefore inputting 300 represents 0.30%
     */
    function setup(
        address _oracle,
        bytes32 _questionId,
        uint _numOutcomes,
        uint _subsidy,
        uint _overround
    ) public onlyOwner {
        require(initialized == false, 'Already initialized');
        require(_overround > 0, 'Cannot have 0 overround');
        CT.prepareCondition(_oracle, _questionId, _numOutcomes);
        condition = CT.getConditionId(_oracle, _questionId, _numOutcomes);

        IERC20(token).safeTransferFrom(msg.sender, address(this), _subsidy);

        numOutcomes = _numOutcomes;
        int128 n = ABDKMath.fromUInt(_numOutcomes);
        int128 initial_subsidy = getTokenEth(token, _subsidy);

        int128 overround = ABDKMath.divu(_overround, 10000); // Note: if the overround is too low, then exp overflows
        alpha = ABDKMath.div(overround, ABDKMath.mul(n, ABDKMath.ln(n)));
        b = ABDKMath.mul(ABDKMath.mul(initial_subsidy, n), alpha);

        for (uint i = 0; i < _numOutcomes; i++) {
            q.push(initial_subsidy);
        }

        initialized = true;
        totalShares = ABDKMath.mul(initial_subsidy, n);
        currentCost = cost();
    }

    /**
     * @notice This function is used to buy outcome tokens.
     * @param _outcome The outcome(s) which a user is buying tokens for.
     * Note: This is the integer representation for the bit array.
     * @param _amount This is the number of outcome tokens purchased
     * @return _price The cost to purchase _amount number of tokens
     */
    function buy(uint256 _outcome, int128 _amount) public onlyAfterInit returns (int128 _price) {
        // If enabled, check if market already started
        if (startTimestamp > 0) {
            require(block.timestamp >= startTimestamp, 'Market not started');
        }

        // If enabled, check if market still open
        if (endTimestamp > 0) {
            require(block.timestamp <= endTimestamp, 'Market already ended');
        }

        // Send call to internal
        return _buy(_outcome, _amount, msg.sender);
    }

    function _buy(uint256 _outcome, int128 _amount, address account) public onlyOwnerOrSelf returns (int128 _price) {
        // Special buy function to Buy tokens f
        require(_outcome > 0, 'Invalid outcome');
        require(CT.payoutDenominator(condition) == 0, 'Market already resolved');

        for (uint j = 0; j < numOutcomes; j++) {
            // Note: (1 << j) is equal to (1 x 2 ** j)
            if ((_outcome & (1 << j)) != 0) {
                q[j] = ABDKMath.add(q[j], _amount);
                totalShares = ABDKMath.add(totalShares, _amount);
            }
        }

        // Calculate new Beta parameter (taking into account new total_shares)
        b = ABDKMath.mul(totalShares, alpha);
        totalBuys += 1;

        int128 sum_total;
        for (uint i = 0; i < numOutcomes; i++) {
            sum_total = ABDKMath.add(sum_total, ABDKMath.exp(ABDKMath.div(q[i], b)));
        }

        int128 new_cost = ABDKMath.mul(b, ABDKMath.ln(sum_total));
        _price = ABDKMath.sub(new_cost, currentCost);
        currentCost = new_cost;

        uint token_cost = getTokenWei(token, _price);
        uint outcomeShares = getTokenWei(token, _amount);
        uint position = CT.getPositionId(IERC20(token), CT.getCollectionId(bytes32(0), condition, _outcome));

        // Get token_cost amount of token from sender (as current payment)
        require(IERC20(token).transferFrom(msg.sender, address(this), token_cost), 'Error transferring tokens');

        if (CT.balanceOf(address(this), position) < outcomeShares) {
            IERC20(token).approve(address(CT), getTokenWei(token, _amount));
            CT.splitPosition(
                IERC20(token), bytes32(0), condition, getPositionAndDustPositions(_outcome), outcomeShares
            );
        }
        // Give n_outcome_tokens of shares to current account (as shares bought)
        CT.safeTransferFrom(address(this), account, position, outcomeShares, '');

        // Register account buy
        accountShares[account].buys += 1;
        if (_outcome == 1) {
            accountShares[account].outcomeOneBalance += outcomeShares;
        }
        if (_outcome == 2) {
            accountShares[account].outcomeTwoBalance += outcomeShares;
        }
        accountShares[account].deposited += token_cost;
    }

    function updateDates(uint256 _startTimestamp, uint256 _endTimestamp) public onlyOwner {
        startTimestamp = _startTimestamp;
        endTimestamp = _endTimestamp;
    }

    function withdraw() public onlyAfterInit onlyOwner {
        require(CT.payoutDenominator(condition) != 0, 'Market needs to be resolved');
        uint[] memory dust = new uint256[](numOutcomes);
        for (uint i = 0; i < numOutcomes; i++) {
            dust[i] = 1 << i;
        }
        CT.redeemPositions(IERC20(token), bytes32(0), condition, dust);
        IERC20(token).safeTransfer(msg.sender, IERC20(token).balanceOf(address(this)));
    }

    function getOnes(uint n) internal pure returns (uint count) {
        while (n != 0) {
            n = n & (n - 1);
            count++;
        }
        return count;
    }

    function getPositionAndDustPositions(uint _outcome) public view returns (uint256[] memory) {
        uint index = (1 << numOutcomes) - 1;
        uint inv = _outcome ^ index;
        uint[] memory partx = new uint256[](getOnes(inv) + 1);
        uint n = 1;
        partx[0] = _outcome;
        for (uint i = 0; i < numOutcomes; i++) {
            // Note: (1 << i) is equal to (1 x 2 ** i)
            if ((inv & 1 << i) != 0) {
                partx[n] = 1 << i;
                n++;
            }
        }
        return partx;
    }

    /**
     *  @notice View function returning the cost function.
     *  This function returns the cost for this inventory state. It will be able
     *  to tell you the total amount of collateral spent within the market maker.
     *  For example, if a pool was seeded with 100 DAI and then a further 20 DAI
     *  has been spent, this function will return 120 DAI.
     */
    function cost() public view onlyAfterInit returns (int128) {
        int128 sum_total;
        for (uint i = 0; i < numOutcomes; i++) {
            sum_total = ABDKMath.add(sum_total, ABDKMath.exp(ABDKMath.div(q[i], b)));
        }
        return ABDKMath.mul(b, ABDKMath.ln(sum_total));
    }

    /**
     *  This function will tell you the cost (similar to above) after a proposed transaction.
     */
    function cost_after_buy(uint256 _outcome, int128 _amount) public view returns (int128) {
        int128 sum_total;
        int128[] memory newq = new int128[](q.length);
        int128 TB = totalShares;

        for (uint j = 0; j < numOutcomes; j++) {
            // Note: (1 << j) is equal to (1 x 2 ** j)
            if ((_outcome & (1 << j)) != 0) {
                newq[j] = ABDKMath.add(q[j], _amount);
                TB = ABDKMath.add(TB, _amount);
            } else {
                newq[j] = q[j];
            }
        }

        int128 _b = ABDKMath.mul(TB, alpha);

        for (uint i = 0; i < numOutcomes; i++) {
            sum_total = ABDKMath.add(sum_total, ABDKMath.exp(ABDKMath.div(newq[i], _b)));
        }

        return ABDKMath.mul(_b, ABDKMath.ln(sum_total));
    }

    /**
     *  This function tells you how much it will cost to make a particular trade.
     *  It does this by calculating the difference between the current cost and the cost after the transaction.
     */
    function price(uint256 _outcome, int128 _amount) public view returns (int128) {
        return cost_after_buy(_outcome, _amount) - currentCost;
    }

    function getMarketInfo() public view returns (int128, int128, int128, int128, uint256, int128) {
        if (q.length < 1) {
            return (0, 0, 0, 0, 0, 0);
        }
        return (totalShares, q[0], q[1], currentCost, totalBuys, b);
    }

    function getConditionalTokens() public view returns (address) {
        return address(CT);
    }

    function getTokenWei(address _token, int128 _amount) internal view returns (uint) {
        uint d = ERC20(_token).decimals();
        return ABDKMath.mulu(_amount, 10 ** d);
    }

    function getTokenEth(address _token, uint _amount) internal view returns (int128) {
        uint d = ERC20(_token).decimals();
        return ABDKMath.divu(_amount, 10 ** d);
    }

    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external override pure returns (bytes4) {
        return this.onERC1155Received.selector;
    }

    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external override pure returns (bytes4) {
        return this.onERC1155BatchReceived.selector;
    }

    function supportsInterface(bytes4 interfaceId) external override pure returns (bool) {
        return false;
    }
}
